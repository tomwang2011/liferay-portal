import java.util.jar.Attributes
import java.util.jar.JarFile

task writeReleases

dependencies {
	compile group: "com.liferay", name: "com.liferay.blogs.web", version: "1.0.0"
	compile group: "com.liferay", name: "com.liferay.document.library.api", version: "3.0.0"
	compile group: "com.liferay", name: "com.liferay.document.library.web", version: "1.0.0"
	compile group: "com.liferay", name: "com.liferay.dynamic.data.lists.api", version: "2.0.0"
	compile group: "com.liferay", name: "com.liferay.dynamic.data.lists.service", version: "1.0.0"
	compile group: "com.liferay", name: "com.liferay.dynamic.data.lists.web", version: "1.0.0"
	compile group: "com.liferay", name: "com.liferay.dynamic.data.mapping.api", version: "3.0.0"
	compile group: "com.liferay", name: "com.liferay.dynamic.data.mapping.service", version: "2.0.0"
	compile group: "com.liferay", name: "com.liferay.dynamic.data.mapping.web", version: "1.0.0"
	compile group: "com.liferay", name: "com.liferay.login.web", version: "1.0.0"
	compile group: "com.liferay", name: "com.liferay.message.boards.api", version: "3.0.0"
	compile group: "com.liferay", name: "com.liferay.message.boards.web", version: "1.0.0"
	compile group: "com.liferay.portal", name: "com.liferay.util.java", version: "2.0.0"
	compile group: "javax.portlet", name: "portlet-api", version: "2.0"
	compile project(":apps:collaboration:blogs:blogs-api")
	compile project(":apps:collaboration:blogs:blogs-service")
	compile project(":apps:collaboration:subscription:subscription-api")
	compile project(":apps:collaboration:subscription:subscription-service")
	compile project(":apps:collaboration:wiki:wiki-api")
	compile project(":apps:collaboration:wiki:wiki-service")
	compile project(":apps:foundation:friendly-url:friendly-url-api")
	compile project(":apps:foundation:friendly-url:friendly-url-service")
	compile project(":apps:web-experience:journal:journal-api")
	compile project(":apps:web-experience:journal:journal-service")

	provided group: "com.liferay", name: "org.freemarker", version: "2.3.23.LIFERAY-PATCHED-4"
	provided group: "com.liferay.portal", name: "com.liferay.portal.impl", version: "default"
	provided group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "default"

	testCompile group: "com.liferay", name: "com.ibm.icu4j", version: "54.1.1.LIFERAY-PATCHED-2"
	testCompile group: "com.liferay", name: "com.liferay.osgi.service.tracker.collections", version: "2.0.0"
	testCompile group: "com.liferay", name: "com.liferay.petra.concurrent", version: "1.0.0"
	testCompile group: "com.liferay", name: "com.liferay.petra.lang", version: "1.0.0"
	testCompile group: "com.liferay", name: "com.liferay.petra.log4j", version: "1.0.0"
	testCompile group: "com.liferay", name: "com.liferay.petra.memory", version: "1.0.0"
	testCompile group: "com.liferay", name: "com.liferay.petra.nio", version: "1.0.0"
	testCompile group: "com.liferay", name: "com.liferay.registry.api", version: "1.1.0"
	testCompile group: "com.liferay", name: "com.sun.syndication", version: "1.0.LIFERAY-PATCHED-1"
	testCompile group: "com.liferay", name: "org.jabsorb", version: "1.3.1.LIFERAY-PATCHED-1"
	testCompile group: "com.mchange", name: "c3p0", version: "0.9.5"
	testCompile group: "com.redhat.qe", name: "json-java", version: "20110202"
	testCompile group: "commons-io", name: "commons-io", version: "2.5"
	testCompile group: "easyconf", name: "easyconf", version: "0.9.5"
	testCompile group: "log4j", name: "apache-log4j-extras", version: "1.1"
	testCompile group: "org.apache.tika", name: "tika-core", version: "1.14"
	testCompile group: "org.apache.tomcat", name: "tomcat-jdbc", version: "8.0.32"
	testCompile group: "org.hsqldb", name: "hsqldb:2.3.3", version: "jdk6debug"
	testCompile group: "org.jodd", name: "jodd-json", version: "3.6.4"
	testCompile group: "org.slf4j", name: "slf4j-api", version: "1.7.2"
	testCompile group: "org.springframework", name: "spring-context", version: "4.1.9.RELEASE"
}

classes {
	dependsOn writeReleases
}

writeReleases {
	dependsOn configurations.compile

	doLast {
		File releasesTxtFile = new File(processResources.destinationDir, "com/liferay/portal/tools/sample/sql/builder/dependencies/releases.txt")

		releasesTxtFile.withWriter("UTF-8") {
			Writer writer ->

			configurations.compile.each {
				println "Adding release information from ${it} into ${releasesTxtFile}"

				JarFile jarFile = new JarFile(it)

				Attributes mainAttributes = jarFile.manifest.mainAttributes

				String schemaVersion = mainAttributes.getValue("Liferay-Require-SchemaVersion")

				if (!schemaVersion) {
					return
				}

				String symbolicName = mainAttributes.getValue("Bundle-SymbolicName")

				writer.writeLine "${symbolicName}:${schemaVersion}"
			}
		}
	}
}