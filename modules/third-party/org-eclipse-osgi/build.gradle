task jarPatched(type: Zip)

sourceCompatibility = "1.6"
targetCompatibility = "1.6"

dependencies {
	compileOnly group: "com.liferay", name: "org.eclipse.osgi", version: "3.10.200-20150904.172142-1-LIFERAY-CACHED"
}

jar {
	deleteAllActions()

	dependsOn jarPatched
}

jarPatched {
	archiveName = jar.archiveName
	destinationDir = jar.destinationDir
	duplicatesStrategy = "exclude"

	exclude "META-INF/ECLIPSE_.RSA"
	exclude "META-INF/ECLIPSE_.SF"

	from sourceSets.main.output

	from {
		zipTree(configurations.compileOnly.singleFile)
	}
}

updateFileVersions {
	replaceOnlyIf([{
		String group, String replacement, String content, File contentFile ->

		if (buildFile == contentFile) {
			return false
		}

		return true
	}])
}